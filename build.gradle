plugins {
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'me.shedaniel.unified-publishing' version '0.1.+'
}

version = mod_version
group = mod_group_id

repositories {
    maven { url = "https://nexus.themcbrothers.net/repository/maven-mod-dependencies" }
    mavenLocal()
}

base {
    archivesName = mod_id
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: mapping_channel, version: mapping_version

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // This property allows configuring Gradle's ProcessResources task(s) to run on IDE output locations before launching the game.
    // It is REQUIRED to be set to true for this template to function.
    // See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
    copyIdeResources = true

    runs {
        // applies to all the run configs below
        configureEach {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        data {
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft "net.minecraftforge:forge:$minecraft_version-$forge_version"

    // TheMCBrosLib
    implementation fg.deobf("net.themcbrothers:TheMCBrosLib:$minecraft_version-$lib_version")

    // JEI
    compileOnly fg.deobf("mezz.jei:jei-$minecraft_version-common-api:$jei_version")
    compileOnly fg.deobf("mezz.jei:jei-$minecraft_version-forge-api:$jei_version")
    runtimeOnly fg.deobf("mezz.jei:jei-$minecraft_version-forge:$jei_version")

    // Optional: Configured and Catalogue
    if (!catalogue_file_id.isEmpty()) runtimeOnly fg.deobf("curse.maven:catalogue-459701:$catalogue_file_id")
    if (!configured_file_id.isEmpty()) runtimeOnly fg.deobf("curse.maven:configured-457570:$configured_file_id")
}

tasks.named('processResources', ProcessResources).configure {
    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand project: project
    }
}

// Example for how to get properties into the manifest for reading at runtime.
tasks.named('jar', Jar).configure {
    exclude 'net/themcbrothers/uselessmod/data/**'
    archiveAppendix = minecraft_version
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : jar.archiveVersion,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On-Minecraft'      : minecraft_version
        ])
    }

    finalizedBy 'reobfJar'
}

// Publish on Nexus
publishing {
    publications { PublicationContainer publicationContainer ->
        publicationContainer.register("maven", MavenPublication) { MavenPublication publication ->
            publication.artifacts = [jar]
            publication.version = "$project.minecraft_version-$project.version"
            publication.artifactId = project.maven_name
        }
    }
    repositories {
        maven {
            credentials {
                username = System.getenv("NEXUS_UNAME")
                password = System.getenv("NEXUS_PWD")
            }

            url 'https://nexus.themcbrothers.net/repository/maven-releases/'
        }
    }
}

unifiedPublishing {
    project {
        displayName = "[$minecraft_version] v$mod_version"
        releaseType = project.artifact_type
        gameVersions = [minecraft_version]
        gameLoaders = ["forge", "neoforge"]

        mainPublication tasks.jar

        var curseforgeToken = System.getenv("CURSEFORGE_TOKEN")
        var modrinthToken = System.getenv("MODRINTH_TOKEN")
        var changes = file('changelog.md').text

        if (curseforgeToken != null) {
            curseforge {
                token = curseforgeToken
                id = project.curse_project_id
                changelog = changes
                gameVersions.addAll "Java 17"

                relations {
                    depends "themcbroslib"
                }
            }
        }

        if (modrinthToken != null) {
            modrinth {
                token = modrinthToken
                id = project.modrinth_project_id
                changelog = changes.substring(changes.indexOf('\n'), changes.indexOf('\n\n'))

                relations {
                    depends "themcbroslib"
                }
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
